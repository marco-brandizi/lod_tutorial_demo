PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX efo: <http://www.ebi.ac.uk/efo/>
PREFIX atlas: <http://rdf.ebi.ac.uk/resource/atlas/>
PREFIX atlasterms: <http://rdf.ebi.ac.uk/terms/atlas/>
PREFIX biopax3:<http://www.biopax.org/release/biopax-level3.owl#>
PREFIX cco: <http://rdf.ebi.ac.uk/terms/chembl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

SELECT DISTINCT ?pathway ?pathwayName ?molecule ?moleculeId ?altLabel
WHERE 
{
	# The proteins associated to the condition
  ?specimen 
    atlasterms:dbXref ?dbXrefProt;
    atlasterms:hasFactorValue ?condition.
    
  ?condition rdfs:subClassOf ?searchCondition.

	# Which pathways they're involved
	SERVICE <http://www.ebi.ac.uk/rdf/services/reactome/sparql> 
	{
	  ?protein rdf:type biopax3:Protein.
	  ?protein biopax3:memberPhysicalEntity
	  		[biopax3:entityReference ?dbXrefProt].
	  ?pathway biopax3:displayName ?pathwayName.
	  ?pathway biopax3:pathwayComponent ?reaction.
	  ?reaction ?rel ?protein
	}

	# Compunds that target such pathways 
	SERVICE <http://www.ebi.ac.uk/rdf/services/chembl/sparql>
	{
	  ?act 
	    a cco:Activity;
	    cco:hasMolecule ?molecule;
	    cco:hasAssay ?assay.
	    
	  ?molecule 
	    rdfs:label ?moleculeId;
	    skos:altLabel ?altLabel.
	
	  ?assay cco:hasTarget ?target.
	  ?target cco:hasTargetComponent ?targetcmpt.
	  ?targetcmpt cco:targetCmptXref ?dbXrefProt.
	  ?targetcmpt cco:taxonomy <http://identifiers.org/taxonomy/9606>. # Human
	  ?dbXrefProt a cco:UniprotRef .
	}

} LIMIT 25
